import tactic.omega
import data.fin
import data.fintype.basic
import data.set.finite
import data.set.lattice
import for_mathlib.equiv
import for_mathlib.finvec
import for_mathlib.nat

open set
open_locale finvec

namespace o_minimal

variables (R : Type*)

/-- [vdD:1.2.1] A *structure* on a type R consists of
a class ğ‘†â‚™ of *definable* subsets of Râ¿ for each n â‰¥ 0
such that:
  (S1) ğ‘†â‚™ is a boolean subalgebra of subsets of Râ¿;
  (S2) If A belongs to ğ‘†â‚™ then R Ã— A and A Ã— R belong to ğ‘†â‚™â‚Šâ‚;
  (S3) For n â‰¥ 1, {(xâ‚, ..., xâ‚™) | xâ‚ = xâ‚™} belongs to ğ‘†â‚™;
  (S4) If A belongs to ğ‘†â‚™â‚Šâ‚ then Ï€(A) belongs to Sâ‚™, where
       Ï€ : Râ¿âºÂ¹ â†’ Râ¿ denotes the projection onto the first n coordinates. -/
structure struc :=
-- TODO: "generalize" to Type instead of Prop here?
-- TODO: Try this idea: (definable : (Î£ {n : â„•}, set (R^n)) â†’ Prop)?
(definable : Î  {n : â„•} (A : set (finvec n R)), Prop)
(definable_empty (n : â„•) :
  definable (âˆ… : set (finvec n R)))
(definable_union {n : â„•} {A B : set (finvec n R)} :
  definable A â†’ definable B â†’ definable (A âˆª B))
(definable_compl {n : â„•} {A : set (finvec n R)} :
  definable A â†’ definable Aá¶œ)
(definable_r_prod {n : â„•} {A : set (finvec n R)} :
  definable A â†’ definable (finvec.univ_prod 1 A))
(definable_prod_r {n : â„•} {A : set (finvec n R)} :
  definable A â†’ definable (finvec.prod_univ A 1))
(definable_eq_outer {n : â„•} :
  definable ({x | x 0 = x.last} : set (finvec (n + 1) R)))
(definable_proj1 {n : â„•} {A : set (finvec (n + 1) R)} :
  definable A â†’ definable (finvec.left '' A))

variables {R} (S : struc R)

lemma struc.convert_definable {n n' : â„•}
  {A : set (finvec n R)} {A' : set (finvec n' R)} (hA' : S.definable A')
  (h : n = n') (e : âˆ€ x, x âˆˆ A â†” finvec.cast h x âˆˆ A') :
  S.definable A :=
begin
  cases h,
  convert hA',
  ext x,
  convert e x,
  rw finvec.cast_id
end

lemma struc.definable_univ (n : â„•) : S.definable (univ : set (finvec n R)) :=
begin
  rw â†compl_empty,
  exact S.definable_compl (S.definable_empty n)
end

lemma struc.definable_inter {n : â„•} {A B : set (finvec n R)}
  (hA : S.definable A) (hB : S.definable B) : S.definable (A âˆ© B) :=
begin
  rw inter_eq_compl_compl_union_compl,
  exact S.definable_compl (S.definable_union (S.definable_compl hA) (S.definable_compl hB))
end

lemma struc.definable_diff {n : â„•} {A B : set (finvec n R)}
  (hA : S.definable A) (hB : S.definable B) : S.definable (A \ B) :=
begin
  rw diff_eq,
  exact S.definable_inter hA (S.definable_compl hB)
end

lemma struc.definable_Inter {Î¹ : Type*} [fintype Î¹] {n : â„•} {A : Î¹ â†’ set (finvec n R)}
  (hA : âˆ€ i, S.definable (A i)) : S.definable (â‹‚ i, A i) :=
suffices âˆ€ {s : set Î¹}, set.finite s â†’ S.definable (â‹‚ i âˆˆ s, A i),
by { convert this finite_univ, simp },
Î» s hs, finite.induction_on hs
  (by { convert S.definable_univ _, simp })
  (Î» i s _ _ IH, by { convert S.definable_inter (hA i) IH, simp })

lemma struc.definable_rn_prod {m n : â„•} {A : set (finvec n R)}
  (hA : S.definable A) : S.definable (finvec.univ_prod m A) :=
begin
  induction m using nat.rec_plus_one with m IH,
  { exact (finvec.univ_prod_zero_like _).mpr hA },
  { exact (finvec.univ_prod_plus_one_like _).mpr (S.definable_r_prod IH) }
end

lemma struc.definable_prod_rn {n m : â„•} {A : set (finvec n R)}
  (hA : S.definable A) : S.definable (finvec.prod_univ A m) :=
begin
  induction m with m IH,
  { rw finvec.prod_univ_zero_eq, exact hA },
  { rw finvec.prod_univ_plus_one_eq, exact S.definable_prod_r IH }
end

--- [vdD:1.2.2(i)]
lemma struc.definable_external_prod {n m : â„•}
  {A : set (finvec n R)} (hA : S.definable A)
  {B : set (finvec m R)} (hB : S.definable B) : S.definable (A âŠ  B) :=
S.definable_inter (S.definable_prod_rn hA) (S.definable_rn_prod hB)

private lemma obvious_nat_lemma {n : â„•} {i j : fin n} (h : i â‰¤ j) :
  n = â†‘i + (â†‘j - â†‘i + 1 + (n - 1 - â†‘j)) :=
begin
  cases i with i,
  cases j with j,
  change i â‰¤ j at h,
  dsimp,
  omega
end

--- [vdD:1.2.2(ii)]
lemma struc.definable_eq {n : â„•} (i j : fin n) :
  S.definable {x | x i = x j} :=
begin
  wlog h : i â‰¤ j using i j,
  swap, { convert this, ext x, apply eq_comm },
  have : S.definable
    (finvec.univ_prod i (finvec.prod_univ {x : finvec ((j - i) + 1) R | x 0 = x (fin.last _)} (n - 1 - j))),
  { apply S.definable_rn_prod,
    apply S.definable_prod_rn,
    apply S.definable_eq_outer },
  apply S.convert_definable this (obvious_nat_lemma h),
  intro x,
  simp only
    [finvec.mem_prod_iff, true_and, and_true, mem_univ, mem_set_of_eq, function.comp_app],
  congr'; ext,
  { simp, refl },
  { simpa using (nat.add_sub_cancel' h).symm }
end

lemma struc.definable_diag_rn {n : â„•} :
  S.definable {x : finvec (n + n) R | x.left = x.right} :=
begin
  have : S.definable {x : finvec (n + n) R | âˆ€ (i : fin n), x.left i = x.right i},
  { convert_to S.definable (â‹‚ (i : fin n), {x : finvec (n + n) R | x.left i = x.right i}),
    { ext, simp },
    exact S.definable_Inter (Î» i, S.definable_eq _ _) },
  convert this,
  ext x,
  apply function.funext_iff
end

lemma struc.definable_proj (S : struc R) {n m : â„•} {A : set (finvec (n + m) R)}
  (hA : S.definable A) : S.definable (finvec.left '' A) :=
begin
  induction m with m IH,
  -- Defeqs are in our favor, so just use `convert`.
  { convert hA,
    convert set.image_id _,
    ext x âŸ¨i,_âŸ©,
    refl },
  { convert IH (S.definable_proj1 hA) using 1,
    rw â†set.image_comp,
    refl }
end

lemma struc.definable_reindex_aux {n m : â„•} (Ïƒ : fin n â†’ fin m)
  {B : set (finvec n R)} (hB : S.definable B) :
  S.definable {x : finvec (m + n) R | x.left âˆ˜ Ïƒ = x.right âˆ§ x.right âˆˆ B} :=
begin
  let Z : set (finvec (m + n) R) :=
    (â‹‚ (i : fin n), {z | z ((Ïƒ i).cast_add _) = z (i.nat_add _)}) âˆ© finvec.univ_prod m B,
  have Zdef : âˆ€ (x : finvec m R) (y : finvec n R), x ++ y âˆˆ Z â†” x âˆ˜ Ïƒ = y âˆ§ y âˆˆ B,
  { intros x y,
    simp only [set.mem_inter_iff, set.mem_Inter, set.mem_univ, finvec.univ_prod_eq,
      finvec.append_mem_prod_iff, â†and_assoc, and_true],
    congr',
    rw [eq_iff_iff, function.funext_iff],
    apply forall_congr,
    intro j,
    change (x ++ y).left (Ïƒ j) = (x ++ y).right j â†” _,
    simp },
  have : S.definable Z :=
    S.definable_inter (S.definable_Inter $ Î» i, S.definable_eq _ _) (S.definable_rn_prod hB),
  convert this,
  ext z,
  dsimp,
  specialize Zdef (finvec.left z) (finvec.right z),
  rw finvec.left_append_right at Zdef,
  simp at Zdef,
  simp [Zdef],
  exact iff.rfl,
end

--- [vdD:1.2.2(iii)]
lemma struc.definable_reindex {n m : â„•} (Ïƒ : fin n â†’ fin m)
  {B : set (finvec n R)} (hB : S.definable B) : S.definable {x | x âˆ˜ Ïƒ âˆˆ B} :=
begin
  convert (S.definable_proj (S.definable_reindex_aux Ïƒ hB)),
  ext x,
  rw â†finvec.append_equiv.symm.exists_congr_left,
  rw prod.exists,
  simp only [finvec.append_equiv_symm_app, finvec.left_append],
  finish []
end

end o_minimal
